     A                                      DSPSIZ(24 80 *DS3)      
     A                                      PRINT                   
     A                                      CF03(03 'EXIT')         
     A                                      CF05(05 'YENILE')       
     A                                      CF06(06 'YENI SIPARIS') 
     A* -- SUBFILE SATIR FORMATI (SFL) --                           
     A          R SIP01S                    SFL                     
     A            S01SEL         1A  B  7  2                        
     A            S01SIP         7A  O  7  6                        
     A            S01MUS        25A  O  7 15                        
     A            S01TAR         8Y 0O  7 42EDTWRD('    /  /  ')    
     A            S01TUT        13Y 2O  7 55EDTCDE(K)               
     A*                                                             
     A* -- SUBFILE KONTROL FORMATI (SFLCTL) --                      
     A          R SIP01C                    SFLCTL(SIP01S)          
     A                                      SFLSIZ(0011)            
     A                                      SFLPAG(0010)            
     A                                      OVERLAY            
     A  31                                  SFLDSP             
     A  31                                  SFLDSPCTL          
     A  32                                  SFLCLR             
     A  33                                  SFLEND(*MORE)      
     A                                  1  2'PROG: SIPR01'     
     A                                  1 30'SIPARIS PANELI'   
     A                                      DSPATR(HI)         
     A                                  3  2'ARAMA (SÝP.NO):'  
     A            S01POS         7A  B  3 18                   
     A                                  5  2'SEC'              
     A                                      COLOR(TRQ)         
     A                                  5  6'SÝP.NO'           
     A                                      COLOR(TRQ)         
     A                                  5 15'MUSTERÝ ADÝ'      
     A                                      COLOR(TRQ)         
     A                                  5 42'TARÝH'            
     A                                      COLOR(TRQ)         
     A                                  5 55'TUTAR'            
     A                                      COLOR(TRQ)         
     A          R ENTREC                                           
     A                                      OVERLAY                
     A                                  1 25'SIPARIS KAYIT FORMU'  
     A                                       DSPATR(HI)            
     A                                  5  2'SIPARIS NO:'          
     A            E01SIP         7A  B  5 15                       
     A                                  7  2'MUSTERI ADI:'         
     A            E01MUS        40A  B  7 15                       
     A                                  9  2'TARIH:'               
     A            E01TAR         8Y 0B  9 15                       
     A                                 23  2'F3=IPTAL ENTER=KAYDET'
     A                                      COLOR(BLU)             
     A            E01TUT        13Y 2B 11 15                       
     A* -- ALT BILGI FORMATI (AYRI KAYIT) --                       
     A          R FOOTER                                           
     A*%%TS  SD  20260106  120309  WUSR2224    REL-V7R2M0  5770-WDS
     A                                      OVERLAY                
     A                                 23 23'F6Yeni '              
     A                                      COLOR(BLU)             
     A                                 21  2'2=Duzenle'            
     A                                      COLOR(BLU)         
     A                                 23  2'F3=Cýkýs'         
     A                                      COLOR(BLU)         
     A                                 21 14'4=Sil'            
     A                                      COLOR(BLU)         
     A                                 21 22'5=Detay'          
     A                                      COLOR(BLU)         
     A                                 23 12'F5=Yenile'        
     A                                      COLOR(BLU)         
     A* -- DETAY PENCERESI (WINDOW) --                         
     A          R DETREC                                       
     A                                      WINDOW(6 10 10 55) 
     A                                      OVERLAY            
     A                                  1 12'SIPARIS DETAYLARI'
     A                                      DSPATR(HI)         
     A                                  3  2'SÝPARÝS NO:'      
     A            W01SIP         7A  O  3 15                   
     A                                  5  2'URUN KODU'        
     A                                  5 20'MÝKTAR'           
     A                                  5 35'FÝYAT'            
     A            W01UR1        15A  O  6  2                  
     A            W01MK1        11Y 2O  6 20                  
     A            W01FY1        11Y 2O  6 35                  
     A            W01UR2        15A  O  7  2                  
     A            W01MK2        11Y 2O  7 20                  
     A            W01FY2        11Y 2O  7 35                  
     A                                  9  2'ENTER=KAPAT'     
     A                                      COLOR(BLU)        
